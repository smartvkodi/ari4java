plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = 'ch.loway.oss.ari4java'
version = '0.8.1'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    main {
        java {
            srcDir 'src/main/generated'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.10.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.10.1'
    compile 'io.netty:netty-all:4.1.44.Final'
    compile 'javax.xml.bind:jaxb-api:2.3.1'

    testCompile 'junit:junit:4.10'
}

compileJava.dependsOn ':codegen:runCodegen'
compileJava.dependsOn 'buildProps'

def build_number = 'x'
if (System.getenv('BUILD_NUMBER') != null) {
    build_number = System.getenv('BUILD_NUMBER')
}
task buildProps(type: WriteProperties) {
    outputFile file('src/main/resources/build.properties')
    property 'BUILD_NUMBER', build_number
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def projectUrl = 'http://github.com/l3nz/ari4java'
def pomConfig = {
    licenses {
        license {
            name 'LPGL-3.0'
        }
    }
    developers {
        developer {
            name 'lenz e.'
            email 'nomail@home'
        }
    }
    scm {
        url projectUrl
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId = "ch.loway.oss.ari4java"
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Asterisk ARI interface bindings for Java')
                root.appendNode('name', project.name)
                root.appendNode('url', projectUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key =  System.getenv('BINTRAY_KEY')
    publications = ['mavenPublication']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        group = "ch.loway.oss.ari4java"
        userOrg = 'ari4java'
        licenses = ['LGPL-3.0']
        vcsUrl = projectUrl
        version {
            name = project.version
            desc = project.version
            released = new Date()
        }
    }
}
